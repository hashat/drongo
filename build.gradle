buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.javamodularity:moduleplugin:1.6.0"
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

def javamodularityPluginId = 'org.javamodularity.moduleplugin'
final hasPlugin = project.getPlugins().hasPlugin(javamodularityPluginId);
if(hasPlugin) {
    final Plugin plugin = project.getPlugins().getPlugin(javamodularityPluginId)
    println 'Plugin already applied - version ' + plugin.properties['javamodularityPluginId']
} else {
    apply plugin: "org.javamodularity.moduleplugin"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

group 'com.sparrowwallet'
version '0.9'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation ('org.zeromq:jeromq:0.5.0') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    implementation ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
        exclude group: 'junit', module: 'junit'
    }
    implementation ('org.bouncycastle:bcprov-jdk15on:1.64') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    implementation ('de.mkammerer:argon2-jvm:2.7') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
        exclude group: 'junit', module: 'junit'
    }
    implementation ('ch.qos.logback:logback-classic:1.2.3') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation ('junit:junit:4.12') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: '2.2'
}

task(runDrongo, dependsOn: 'classes', type: JavaExec) {
    main = 'com.sparrowwallet.drongo.Main'
    classpath = sourceSets.main.runtimeClasspath
    args 'drongo.properties'
}

jar {
    manifest {
        attributes "Main-Class": "com.sparrowwallet.drongo.Main"
    }

    exclude('logback.xml')

    archiveBaseName = 'drongo'
    archiveVersion = '0.9'
}

shadowJar {
    archiveVersion = '0.9'
    classifier = 'all'
}
